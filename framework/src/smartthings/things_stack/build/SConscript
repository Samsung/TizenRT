##
# This script includes generic build options:
#    release/debug, target os, target arch, cross toolchain, build environment etc
##
import os
import platform

# Map of host os and allowed target os (host: allowed target os)
host_target_map = {
		'linux': ['linux', 'android', 'arduino', 'yocto', 'tizen'],
		'windows': ['windows', 'winrt', 'android', 'arduino'],
		'darwin': ['darwin', 'ios', 'android', 'arduino'],
		}

# Map of os and allowed archs (os: allowed archs)
os_arch_map = {
		'linux': ['x86', 'x86_64', 'arm', 'arm64', 'mips', 'fsl', 'armgnueabi'],
		'tizen': ['x86', 'x86_64', 'arm', 'arm64', 'armeabi-v7a'],
		'android': ['x86', 'x86_64', 'armeabi', 'armeabi-v7a', 'armeabi-v7a-hard', 'arm64-v8a'],
		'windows': ['x86', 'amd64', 'arm'],
		'winrt': ['arm'],
		'darwin': ['i386', 'x86_64'],
		'ios': ['i386', 'x86_64', 'armv7', 'armv7s', 'arm64'],
		'arduino': ['avr', 'arm'],
		'yocto': ['i586', 'x86_64', 'arm', 'powerpc', 'powerpc64', 'mips', 'mipsel'],
		}

host = platform.system().lower()

if not host_target_map.has_key(host):
	print "\nError: Current system (%s) isn't supported\n" % host
	Exit(1)

######################################################################
# Get build options (the optins from command line)
######################################################################
target_os = ARGUMENTS.get('TARGET_OS', host).lower() # target os

if target_os not in host_target_map[host]:
	print "\nError: Unknown target os: %s (Allow values: %s)\n" % (target_os, host_target_map[host])
	Exit(1)

if target_os == 'android':
	default_arch = 'x86'
else:
	default_arch = platform.machine()

if default_arch not in os_arch_map[target_os]:
	default_arch = os_arch_map[target_os][0].lower()

target_arch = ARGUMENTS.get('TARGET_ARCH', default_arch) # target arch

# True if binary needs to be installed on board. (Might need root permissions)
# set to 'no', 'false' or 0 for only compilation
require_upload = ARGUMENTS.get('UPLOAD', False)

# Get the device name. This name can be used as network display name wherever possible
device_name = ARGUMENTS.get('DEVICE_NAME', "OIC-DEVICE")

if ARGUMENTS.get('TEST'):
	logging_default = False
else:
	release_mode = False
	if ARGUMENTS.get('RELEASE', True) in ['y', 'yes', 'true', 't', '1', 'on', 'all', True]:
		release_mode = True
	logging_default = (release_mode == False)



######################################################################
# Common build options (release, target os, target arch)
######################################################################
targets_disallow_multitransport = ['arduino', 'android']

help_vars = Variables()
help_vars.Add(BoolVariable('VERBOSE', 'Show compilation', False))
help_vars.Add(BoolVariable('RELEASE', 'Build for release?', True)) # set to 'no', 'false' or 0 for debug
help_vars.Add(EnumVariable('TARGET_OS', 'Target platform', host, host_target_map[host]))
help_vars.Add(BoolVariable('FT_ON', 'File Transfer on', False))


help_vars.Add(BoolVariable('WITH_RA', 'Build with Remote Access module', False))

if target_os in targets_disallow_multitransport:
    help_vars.Add(ListVariable('TARGET_TRANSPORT', 'Target transport', 'IP', ['BT', 'BLE', 'IP']))
else:
    help_vars.Add(ListVariable('TARGET_TRANSPORT', 'Target transport', 'ALL', ['ALL', 'BT', 'BLE', 'IP']))

help_vars.Add(ListVariable('TARGET_DEVICE', 'Target device', 'ALL', ['ALL', 'ac', 'robotcleaner', 'fridge','washer','refrigerator','aircleaner', 'oven','controller' ]))

help_vars.Add(ListVariable('DEVICE_VERSION', 'Device Version', 'LITE', ['RICH', 'LITE']))

help_vars.Add(EnumVariable('TARGET_ARCH', 'Target architecture', default_arch, os_arch_map[target_os]))
help_vars.Add(EnumVariable('SECURED', 'Build with DTLS', '0', allowed_values=('0', '1', '2')))
help_vars.Add(EnumVariable('SVR_ENC', 'Enable SVR DB encryption', '0', allowed_values=('0', '1')))
help_vars.Add(EnumVariable('TEST', 'Run unit tests', '0', allowed_values=('0', '1')))
help_vars.Add(BoolVariable('LOGGING', 'Enable stack logging', logging_default))
help_vars.Add(BoolVariable('UPLOAD', 'Upload binary ? (For Arduino)', require_upload))
help_vars.Add(EnumVariable('BUILD_SAMPLE', 'Build with sample', 'ON', allowed_values=('ON', 'OFF')))
help_vars.AddVariables(('DEVICE_NAME', 'Network display name for device (For Arduino)', device_name, None, None),)
help_vars.Add(PathVariable('ANDROID_NDK', 'Android NDK path', None, PathVariable.PathAccept))
help_vars.Add(PathVariable('ANDROID_HOME', 'Android SDK path', None, PathVariable.PathAccept))
help_vars.Add(PathVariable('ANDROID_GRADLE', 'Gradle binary file', None, PathVariable.PathIsFile))
help_vars.Add(PathVariable('QT_PATH', 'QT path', None, PathVariable.PathAccept))
help_vars.Add(PathVariable('QT_LIBPATH', 'QT Library path', None, PathVariable.PathAccept))
#ES_TARGET_ENROLLEE is for specifying what is our target enrollee (Arduino or rest of platforms which support Multicast)
help_vars.Add(EnumVariable('ES_TARGET_ENROLLEE', 'Target Enrollee', 'arduino', allowed_values=('arduino', 'tizen', 'linux')))
#ES_ROLE is for specifying the role (Enrollee or Mediator) for which scons is being executed
help_vars.Add(EnumVariable('ES_ROLE', 'Target build mode', 'mediator', allowed_values=('mediator', 'enrollee')))
#ES_SOFT_MODE is for specifying MODE (Mode 1 : Enrollee with  Soft AP or Mode 2  : Mediator with Soft AP)
help_vars.Add(EnumVariable('ES_SOFTAP_MODE', 'Target build mode', 'ENROLLEE_SOFTAP', allowed_values=('ENROLLEE_SOFTAP', 'MEDIATOR_SOFTAP')))

AddOption('--prefix',
                  dest='prefix',
                  type='string',
                  nargs=1,
                  action='store',
                  metavar='DIR',
                  help='installation prefix')

######################################################################
# Platform(build target) specific options: SDK/NDK & toolchain
######################################################################
targets_support_cc = ['linux', 'arduino', 'tizen']

if target_os in targets_support_cc:
	# Set cross compile toolchain
	help_vars.Add('TC_PREFIX', "Toolchain prefix (Generally only be required for cross-compiling)", os.environ.get('TC_PREFIX'))
	help_vars.Add(PathVariable('TC_PATH',
			'Toolchain path (Generally only be required for cross-compiling)',
			os.environ.get('TC_PATH')))

if target_os in ['android', 'arduino']: # Android/Arduino always uses GNU compiler regardless of the host
	env = Environment(variables = help_vars,
			tools = ['gnulink', 'gcc', 'g++', 'ar', 'as']
			)
else:
	env = Environment(variables = help_vars, TARGET_ARCH = target_arch, TARGET_OS = target_os, PREFIX = GetOption('prefix'))

Help(help_vars.GenerateHelpText(env))

tc_set_msg = '''
************************************ Warning **********************************
*   Enviornment variable TC_PREFIX/TC_PATH is set. It will change the default *
* toolchain, if it isn't what you expect you should unset it, otherwise it may*
* cause inexplicable errors.                                                  *
*******************************************************************************
'''
if env.get('VERBOSE') == False:
	env['CCCOMSTR'] = "Compiling $TARGET"
	env['SHCCCOMSTR'] = "Compiling $TARGET"
	env['CXXCOMSTR'] = "Compiling $TARGET"
	env['SHCXXCOMSTR'] = "Compiling $TARGET"
	env['LINKCOMSTR'] = "Linking $TARGET"
	env['SHLINKCOMSTR'] = "Linking $TARGET"
	env['ARCOMSTR'] = "Archiving $TARGET"
	env['RANLIBCOMSTR'] = "Indexing Archive $TARGET"

if target_os in targets_support_cc:
	prefix = env.get('TC_PREFIX')
	tc_path = env.get('TC_PATH')
	if prefix:
		env.Replace(CC = prefix + env.get('CC', 'gcc'))
		env.Replace(CXX = prefix + env.get('CXX', 'g++'))
		env.Replace(AR = prefix + env.get('AR', 'ar'))
		env.Replace(AS = prefix + env.get('AS', 'as'))
		env.Replace(RANLIB = prefix + env.get('RANLIB', 'ranlib'))

	if tc_path:
		env.PrependENVPath('PATH', tc_path)
		if target_arch == 'fsl':
			sys_root = os.path.abspath(tc_path + '/../arm-fsl-linux-gnueabi/multi-libs/')
		elif target_arch == 'armgnueabi':
			sys_root = os.path.abspath(tc_path + '/../arm-linux-gnueabi/libc')
		else:
			sys_root = os.path.abspath(tc_path + '/../')
		env.AppendUnique(CCFLAGS = ['--sysroot=' + sys_root])
		env.AppendUnique(LINKFLAGS = ['--sysroot=' + sys_root])

	if prefix or tc_path:
		print tc_set_msg

# Ensure scons be able to change its working directory
env.SConscriptChdir(1)

# Set the source directory and build directory
#   Source directory: 'dir'
#   Build directory: 'dir'/out/<target_os>/<target_arch>/<release or debug>/
#
# You can get the directory as following:
#   env.get('SRC_DIR')
#   env.get('BUILD_DIR')

def __set_dir(env, dir):
	if not os.path.exists(dir + '/SConstruct'):
		print '''
*************************************** Error *********************************
* The directory(%s) seems isn't a source code directory, no SConstruct file is
* found. *
*******************************************************************************
''' % dir
		Exit(1)

	if env.get('RELEASE'):
		build_dir = dir + '/out/' + target_os + '/' + target_arch + '/release/'
	else:
		build_dir = dir + '/out/' + target_os + '/' + target_arch + '/debug/'
	env.VariantDir(build_dir, dir, duplicate=0)

	env.Replace(BUILD_DIR = build_dir)
	env.Replace(SRC_DIR = dir)

def __src_to_obj(env, src, home = ''):
	obj = env.get('BUILD_DIR') + src.replace(home, '')
	if env.get('OBJSUFFIX'):
		obj += env.get('OBJSUFFIX')
	return env.Object(obj, src)

def __install(ienv, targets, name):
	i_n = ienv.Install(env.get('BUILD_DIR'), targets)
	Alias(name, i_n)
	env.AppendUnique(TS = [name])

def __installlib(ienv, targets, name):
	user_prefix = env.get('PREFIX')
	if user_prefix:
		i_n = ienv.Install(user_prefix + '/lib', targets)
	else:
		i_n = ienv.Install(env.get('BUILD_DIR'), targets)
	ienv.Alias("install", i_n)

def __installbin(ienv, targets, name):
	user_prefix = env.get('PREFIX')
	if user_prefix:
		i_n = ienv.Install(user_prefix + '/bin', targets)
	else:
		i_n = ienv.Install(env.get('BUILD_DIR'), targets)
	ienv.Alias("install", i_n)

def __append_target(ienv, name, targets = None):
	if targets:
		env.Alias(name, targets)
	env.AppendUnique(TS = [name])

def __print_targets(env):
	Help('''
===============================================================================
Targets:\n    ''')
	for t in env.get('TS'):
		Help(t + ' ')
	Help('''
\nDefault all targets will be built. You can specify the target to build:

    $ scons [options] [target]
===============================================================================
''')

env.AddMethod(__set_dir, 'SetDir')
env.AddMethod(__print_targets, 'PrintTargets')
env.AddMethod(__src_to_obj, 'SrcToObj')
env.AddMethod(__append_target, 'AppendTarget')
env.AddMethod(__install, 'InstallTarget')
env.AddMethod(__installlib, 'UserInstallTargetLib')
env.AddMethod(__installbin, 'UserInstallTargetBin')
env.SetDir(env.GetLaunchDir())
env['ROOT_DIR']=env.GetLaunchDir()+'/..'

Export('env')

######################################################################
# Link scons to Yocto cross-toolchain ONLY when target_os is yocto
######################################################################
if target_os == "yocto":
    '''
    This code injects Yocto cross-compilation tools+flags into scons'
    build environment in order to invoke the relevant tools while
    performing a build.
    '''
    env.AppendUnique(CCFLAGS = ['-Wall', '-Wextra', '-fPIC'])
    env.AppendUnique(LIBS = ['uuid'])
else:
    '''
    If target_os is not Yocto, continue with the regular build process
    '''
    # Load config of target os
    env.SConscript(target_os + '/SConscript')

# Delete the temp files of configuration
if env.GetOption('clean'):
	dir = env.get('SRC_DIR')

	if os.path.exists(dir + '/config.log'):
		Execute(Delete(dir + '/config.log'))
	if os.path.exists(dir + '/.sconsign.dblite'):
		Execute(Delete(dir + '/.sconsign.dblite'))
	if os.path.exists(dir + '/.sconf_temp'):
		Execute(Delete(dir + '/.sconf_temp'))

######################################################################
# Check for PThreads support
######################################################################
#import iotivityconfig
#from iotivityconfig import *

#conf = Configure(env,
#       custom_tests =
#        {
#            'CheckPThreadsSupport' : iotivityconfig.check_pthreads
#        } )

# Identify whether we have pthreads support, which is necessary for
# threading and mutexes.  This will set the environment variable
# POSIX_SUPPORTED, 1 if it is supported, 0 otherwise
#conf.CheckPThreadsSupport()

#env = conf.Finish()
######################################################################

#env.SConscript('external_libs.scons')
Return('env')
